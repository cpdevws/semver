group 'org.crosslibs'
version '1.0'

apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

repositories {
    jcenter()
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.enabled true
    }
}

groovydoc {
    docTitle = 'Semantic Versioning 2.0 Implementation API<br/>(org.crosslibs.semver)<br/>'
    windowTitle = 'org.crosslibs.semver:1.0 API'
}


task docsjar(type: Jar, dependsOn: groovydoc){
    classifier = 'javadoc'
    from groovydoc.destinationDir
}

task sourcesjar(type: Jar, dependsOn: classes){
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives docsjar
    archives sourcesjar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Semantic Versioning 2.0'
                packaging 'jar'
                description 'Groovy implementation of semantic versioning 2.0 rules'
                url 'https://github.com/cpdevws/semver'

                scm {
                    connection 'scm:git:git://github.com/cpdevws/semver.git'
                    developerConnection 'scm:git:ssh://github.com:cpdevws/semver.git'
                    url 'https://github.com/cpdevws/semver/tree/master'
                }

                licenses {
                    license {
                        name 'MIT License'
                        url 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }

                developers {
                    developer {
                        id 'cpdevws'
                        name 'Chaitanya Prakash N'
                        email 'cpdevws@gmail.com'
                    }
                }
            }
        }
    }
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.10'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}
